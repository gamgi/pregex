
Regex           = _{ SOI ~ AnchorStart? ~ ( Alternation | Expression ) ~ AnchorEnd? ~ EOI }
AnchorStart     =  { "^" }
AnchorEnd       =  { "$" }
Alternation     =  { Expression ~ ( "|" ~ ( Alternation | Expression ) ) }
Expression      = _{ Concats | Concat | Factor }

Concats         =  { ( Concat | Factor ) ~ Expression }
Concat          =  { Factor ~ Factor }
Group           = _{ "(" ~ ( Alternation | Expression ) ~ ")" }

Factor          = _{ Quantified | Group | Token }
Token           = _{ Literal | Dot | Class | Escaped }
Quantified      =  { ( Token | Group ) ~ Quantifier }
Literal         =  { ASCII_ALPHANUMERIC | " " | "-" }
Escaped         = _{ "\\" ~ EscapedLiteral }
EscapedLiteral  = @{ ASCII }
Dot             =  { "." }

Class           = _{ ShortClass | LongClassNeg | LongClass }
ShortClass      =  { "\\w" | "\\s" | "\\d" }
LongClass       =  { "[" ~ CharacterClass ~ Dist? ~ "]" }
LongClassNeg    =  { "[^" ~ CharacterClass ~ Dist? ~ "]" }
PosixClass      =  { "[:digit:]" | "[:space:]" }
CharacterClass  =  { (ShortClass | PosixClass | Literal | Dot | ShortClass)+ }

Quantifier      = _{ ShortQuantifier | LongQuantifier }
ShortQuantifier =  { "+" | "?" | "*" }
LongQuantifier  = _{ "{" ~ ExactQuantifier ~ Dist? ~ "}" }
ExactQuantifier =  { QuantifierParam }
QuantifierParam =  { ASCII_DIGIT* }

Dist            =  { "~" ~ DistName ~ ( "(" ~ DistParams ~ ")" )? }
DistName        =  { ^"Bin" | ^"Ber" | ^"Cat" | ^"Const" | ^"Geo" | ^"Zipf" }
DistParams      = _{ DistParam ~ ("," ~ DistParam)* }
DistParam       = _{ IndexParam | NamedParam }
IndexParam      =  { FLOAT_NUMBER }
NamedParam      =  { ( Literal | Dot) ~ "=" ~ ( FLOAT_NUMBER | ASCII_DIGIT+ ) }


FLOAT_NUMBER    = _{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }