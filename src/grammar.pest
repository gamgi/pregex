
Regex 			= _{ SOI ~ ( Alternation | Expression ) ~ EOI }
Alternation     =  { Expression ~ ( "|" ~ ( Alternation | Expression ) ) }
Expression      = _{ Concats | Concat | Factor }

Concats         =  { ( Concat | Factor ) ~ Expression }
Concat          =  { Factor ~ Factor }
Group           = _{ "(" ~ ( Alternation | Expression ) ~ ")" }

Factor          = _{ Quantified | Group | Token }
Token           = _{ Literal }
Quantified      =  { ( Token | Group ) ~ Quantifier }
Literal         =  { ASCII_ALPHANUMERIC | " " }

Quantifier      = _{ ShortQuantifier | LongQuantifier }
ShortQuantifier =  { "+" | "?" | "*" }
LongQuantifier  = _{ "{" ~ ExactQuantifier ~ QuantifierDist? ~ "}" }
ExactQuantifier =  { QuantifierParam }

QuantifierParam =  { ASCII_DIGIT* }
QuantifierDist  =  { "~" ~ DistName ~ "(" ~ DistParam ~ ")" }
DistName        =  { ^"Geo" | ^"Bin" }
DistParam       =  { ASCII_FLOAT }

ASCII_FLOAT     = _{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }